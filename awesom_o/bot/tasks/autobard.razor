###########################################################################
# Autobard module for Awesom-O bot
# Uses peacemaking and provocation skill automatically on nearest mobs

overhead "AutoBard: starting" 2024

hotkey 'clear target queue'

if not timerexists botSkillTimer
    overhead "AutoBard: error, no skill timer!" 34
    wait 200
    script 'awesom_o\bot\main'
endif

if timer botSkillTimer < 11000
    overhead "AutoBard: waiting on skill timer" 2034
    wait 200
    script 'awesom_o\bot\main'
endif

// search 10 mobs and add uniques to list for range check
// target random automatically applies a range check (set in razor), default 12
removelist 'mobsFound'
createlist 'mobsFound'
for 20
    hotkey 'next non-friendly monster target'
    if insysmsg "no one matching"
        break
    endif
    @setvar! mob lasttarget
    pushlist! 'mobsFound' mob
endfor

# overhead "mobs found" 999

// Agro distance is 10 spaces
removelist 'distances'
createlist 'distances'
pushlist 'distances' 1
pushlist 'distances' 2
pushlist 'distances' 3
pushlist 'distances' 4
pushlist 'distances' 6
pushlist 'distances' 8
pushlist 'distances' 10

// perform range check to find the nearest mobs
removelist 'nearestMobs'
createlist 'nearestMobs'
foreach dist in 'distances'
    foreach mobFound in 'mobsFound'
        if find mobFound any any any dist
            pushlist! 'nearestMobs' mobFound
            poplist 'mobsFound' mobFound
            overhead 'In range' 69 mobFound
        endif
    endfor
endfor

// perform line-of-sight check
removelist 'losMobs'
createlist 'losMobs'
whisper '[Aspect'
wait 100
foreach nearMob in 'nearestMobs'
    waitforgump 2424293173
    gumpresponse 100
    waitfortarget 500
    target nearMob
    wait 100
    if insysmsg 'is not arcane'
        pushlist! 'losMobs' nearMob
        overhead 'LOS Good' 69 nearMob
    else
        overhead 'LOS Bad' 33 nearMob
    endif
    hotkey 'clear target queue'
endfor
gumpclose

unsetvar provoAttacker
unsetvar provoVictim
unsetvar peaceTarget
foreach losMob in 'losMobs'
    overhead index 25
    getlabel losMob mobLabel
    if 'provoked' in mobLabel or 'pacified' in mobLabel
        overhead 'already barded' 2045 losMob
        // set a punching bag
        @setvar! autoProvoVictim losMob
    else
        // provo if more than one mob
        if list 'losMobs' > 1
            if varexist autoProvoAttacker
                @setvar! autoProvoVictim losMob
                break
            else
                @setvar! autoProvoAttacker losMob
            endif
        // peace if there is only one mob
        else
            @setvar! autoPeaceTarget losMob
            break
        endif
    endif
endfor

if varexist autoProvoAttacker and varexist autoProvoVictim
    overhead "Provoking"
    overhead "Attacker" 999 autoProvoAttacker
    overhead "Victim" 999 autoProvoVictim
    useskill 'Provocation'
    waitfortarget 500 
    if insysmsg "What instrument"
        if findtype "bamboo flute" backpack as inny
            target inny
        elseif findtype "tambourine" backpack as inny
            target inny
        elseif findtype "drum" backpack as inny
            target inny
        elseif findtype "lute" backpack as inny
            target inny
        elseif findtype "lap harp" backpack as inny
            target inny
        else
            @setvar! isAutoBardModeOn 0
            @setvar! hasInstrument 0
            emote "Lost my instrument" 34
            wait 200
            script 'awesom_o\bot\main'
        endif
        waitfortarget 500
    endif
    
    if targetexists 
        target autoProvoAttacker
        waitfortarget 500
        target autoProvoVictim
        wait 200
        if insysmsg "you play success"
            settimer botSkillTimer 0
            overhead "AutoBard: provo success"
        elseif insysmsg "t incite that!"
            # reset 1/2 timer for failure or unknowns
            settimer botSkillTimer 5000
            overhead "AutoBard: provo fail" 34
        else
            settimer botSkillTimer 10000
            overhead "AutoBard: unknown provo error" 34
        endif
    else
        overhead "AutoBard: no provo target" 34
    endif
 
    unsetvar autoProvoAttacker
    unsetvar autoProvoVictim
endif

if varexist autoPeaceTarget
    overhead "Peacemaking"
    overhead "Peace target" 999 autoPeaceTarget
    useskill 'Peacemaking'
    waitfortarget 500
    if insysmsg "What instrument"
        if findtype "bamboo flute" backpack as inny
            target inny
        elseif findtype "tambourine" backpack as inny
            target inny
        elseif findtype "drum" backpack as inny
            target inny
        elseif findtype "lute" backpack as inny
            target inny
        elseif findtype "lap harp" backpack as inny
            target inny
        else
            @setvar! isAutoBardModeOn 0
            @setvar! hasInstrument 0
            emote "Lost my instrument"
            wait 200
            script 'awesom_o\bot\main'
        endif
        waitfortarget 500
    endif
    
    if targetexists 
        target autoPeaceTarget                
        wait 200
        if insysmsg "you play success"
            settimer botSkillTimer 0
            overhead "AutoBard: peace success"
        elseif insysmsg "you fail"
            # reset 1/2 timer for failure or unknowns
            settimer botSkillTimer 5000
            overhead "AutoBard: peace fail" 55               
        else
            settimer botSkillTimer 10000
            overhead "AutoBard: unknown peace error" 34
        endif
    else
        overhead "AutoBard: no peace target" 34
    endif
 
    unsetvar autoPeaceTarget
endif

wait 200
script 'awesom_o\bot\main'