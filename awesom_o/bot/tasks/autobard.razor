###########################################################################
# Autobard module for Awesom-O bot
# Uses peacemaking and provocation skill automatically on nearest mobs

overhead "AutoBard: starting" 2024

// clear any target prompt
target backpack

// open aspect for LOS
whisper '[Aspect'

if not timerexists botSkillTimer
    overhead "AutoBard: error, no skill timer!" 34
    wait 200
    script 'awesom_o\bot\main'
endif

if timer botSkillTimer < 11000
    overhead "AutoBard: waiting on skill timer" 2034
    wait 200
    script 'awesom_o\bot\main'
endif

// search 10 mobs and add uniques to list for range check
// target random automatically applies a range check (set in razor), default 12
removelist 'mobsFound'
createlist 'mobsFound'
for 20
    hotkey 'next non-friendly monster target'
    if insysmsg "no one matching"
        break
    endif
    @setvar! mob lasttarget
    pushlist! 'mobsFound' mob
endfor

# overhead "mobs found" 999

// Agro distance is 10 spaces
removelist 'distances'
createlist 'distances'
pushlist 'distances' 1
pushlist 'distances' 2
pushlist 'distances' 3
pushlist 'distances' 4
pushlist 'distances' 6
pushlist 'distances' 8
pushlist 'distances' 10

// perform range check to find the nearest mobs
removelist 'nearestMobs'
createlist 'nearestMobs'
foreach dist in 'distances'
    foreach mobFound in 'mobsFound'
        if find mobFound any any any dist
            pushlist! 'nearestMobs' mobFound
            poplist 'mobsFound' mobFound
            overhead 'In range' 69 mobFound
        endif
    endfor
endfor

// perform line-of-sight check
removelist 'losMobs'
createlist 'losMobs'

foreach nearMob in 'nearestMobs'
    waitforgump 2424293173   
    gumpresponse 100
    waitfortarget 500
    target nearMob
    wait 150
    if insysmsg 'is not arcane'
        pushlist! 'losMobs' nearMob
        overhead 'LOS Good' 69 nearMob
    else
        overhead 'LOS Bad' 33 nearMob
    endif
endfor

// look for barding targets
unsetvar alreadyBardedMob
unsetvar firstBardingTarget
unsetvar secondBardingTarget
foreach losMob in 'losMobs'
    getlabel losMob mobLabel
    if 'provoked' in mobLabel or 'pacified' in mobLabel 
        overhead 'already barded' 69 losMob
        // potential punching bag
        @setvar! alreadyBardedMob losMob
    elseif 'barding break' in mobLabel
        // cannot be a target for barding
        overhead 'barding break' 33 losMob
    else
        if varexist firstBardingTarget
            @setvar! secondBardingTarget losMob
            // found two targets, no need to look further
            break
        else
            // set first target, continue looking for a second target
            @setvar! firstBardingTarget losMob
        endif
    endif
endfor

// Peace or provo based on number of targets available
unsetvar provoAttacker
unsetvar provoVictim
unsetvar peaceTarget
if varexist firstBardingTarget and varexist secondBardingTarget
    @setvar! provoAttacker secondBardingTarget
    @setvar! provoVictim firstBardingTarget
elseif varexist firstBardingTarget and varexist alreadyBardedMob
    @setvar! provoAttacker firstBardingTarget
    @setvar! provoVictim alreadyBardedMob
elseif varexist firstBardingTarget
    @setvar! peaceTarget firstBardingTarget
endif

// Provoke if we have targets
if varexist provoAttacker and varexist provoVictim
    overhead "Provoking"
    overhead "Attacker" 999 provoAttacker
    overhead "Victim" 999 provoVictim
    useskill 'Provocation'
    waitfortarget 500 
    if insysmsg "What instrument"
        if findtype "bamboo flute" backpack as inny
            target inny
        elseif findtype "tambourine" backpack as inny
            target inny
        elseif findtype "drum" backpack as inny
            target inny
        elseif findtype "lute" backpack as inny
            target inny
        elseif findtype "lap harp" backpack as inny
            target inny
        else
            @setvar! isAutoBardModeOn 0
            @setvar! hasInstrument 0
            emote "Lost my instrument" 34
            wait 200
            script 'awesom_o\bot\main'
        endif
        waitfortarget 500
    endif
    
    if targetexists 
        target provoAttacker
        waitfortarget 500
        target provoVictim
        wait 200
        if insysmsg "you play success"
            settimer botSkillTimer 0
            overhead "AutoBard: provo success"
        elseif insysmsg "t incite that!"
            # reset 1/2 timer for failure or unknowns
            settimer botSkillTimer 5000
            overhead "AutoBard: provo fail" 34
        else
            settimer botSkillTimer 10000
            overhead "AutoBard: unknown provo error" 34
        endif
    else
        overhead "AutoBard: no provo target" 34
    endif
 
    unsetvar provoAttacker
    unsetvar provoVictim
endif

// Peace if we have a target
if varexist peaceTarget
    overhead "Peacemaking"
    overhead "Peace target" 999 peaceTarget
    useskill 'Peacemaking'
    waitfortarget 500
    if insysmsg "What instrument"
        if findtype "bamboo flute" backpack as inny
            target inny
        elseif findtype "tambourine" backpack as inny
            target inny
        elseif findtype "drum" backpack as inny
            target inny
        elseif findtype "lute" backpack as inny
            target inny
        elseif findtype "lap harp" backpack as inny
            target inny
        else
            @setvar! isAutoBardModeOn 0
            @setvar! hasInstrument 0
            emote "Lost my instrument"
            wait 200
            script 'awesom_o\bot\main'
        endif
        waitfortarget 500
    endif
    
    if targetexists 
        target peaceTarget                
        wait 200
        if insysmsg "you play success"
            settimer botSkillTimer 0
            overhead "AutoBard: peace success"
        elseif insysmsg "you fail"
            # reset 1/2 timer for failure or unknowns
            settimer botSkillTimer 5000
            overhead "AutoBard: peace fail" 55               
        else
            settimer botSkillTimer 10000
            overhead "AutoBard: unknown peace error" 34
        endif
    else
        overhead "AutoBard: no peace target" 34
    endif
 
    unsetvar peaceTarget
endif

wait 200
script 'awesom_o\bot\main'